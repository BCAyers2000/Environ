!
! Copyright (C) 2007-2008 Quantum-ESPRESSO group
! This file is distributed under the terms of the
! GNU General Public License. See the file `License'
! in the root directory of the present distribution,
! or http://www.gnu.org/copyleft/gpl.txt .
!
! original version by O. Andreussi and N. Marzari (MIT)
!
MODULE environ_debug

  USE environ_types

PRIVATE

PUBLIC :: write_cube
!
CONTAINS
!
!--------------------------------------------------------------------
      SUBROUTINE write_cube( ions, f )
!--------------------------------------------------------------------
      !
      USE kinds,          ONLY : DP
      USE io_global,      ONLY : ionode
      USE mp,             ONLY : mp_sum
      USE mp_bands,       ONLY : intra_bgrp_comm
      USE fft_base,       ONLY : dfftp
! BACKWARD COMPATIBILITY
! Compatible with QE-5.1 QE-5.1.1 QE-5.1.2
!      USE fft_base,       ONLY : grid_gather
! Compatible with QE-5.2 QE-5.2.1
!      USE fft_base,       ONLY : gather_grid
! Compatible with QE-5.3 svn
      USE scatter_mod,    ONLY : gather_grid
! END BACKWARD COMPATIBILITY
      !
      IMPLICIT NONE
      !
      TYPE( environ_ions ), TARGET, INTENT(IN) :: ions
      TYPE( environ_density ), TARGET, INTENT(IN) :: f
      !
      INTEGER                  :: ir, ir1, ir2, ir3, num
      INTEGER                  :: ipol, iat, typ, count
      INTEGER                  :: nr1x, nr2x, nr3x
      INTEGER                  :: nr1, nr2, nr3
      !
      REAL( DP )               :: tmp, scale
      REAL( DP ), ALLOCATABLE  :: flocal( : )
      REAL( DP ), DIMENSION(3) :: origin
      !
      CHARACTER( LEN=80 ) :: filename
      REAL( DP ), POINTER :: alat
      REAL( DP ), DIMENSION(:,:), POINTER :: at
      !
      INTEGER, POINTER :: nat
      INTEGER, DIMENSION(:), POINTER :: ityp
      REAL( DP ), DIMENSION(:,:), POINTER :: tau
      !
      nr1x = dfftp%nr1x
      nr2x = dfftp%nr2x
      nr3x = dfftp%nr3x
      !
      nr1 = dfftp%nr1
      nr2 = dfftp%nr2
      nr3 = dfftp%nr3
      !
      filename = f%label
      !
      alat => f%cell%alat
      at => f%cell%at
      !
      nat => ions%number
      ityp => ions%ityp
      tau => ions%tau
      !
      ALLOCATE( flocal( nr1x*nr2x*nr3x ) )
#ifdef __MPI
      flocal = 0.D0
!Compatible with QE-5.1 QE-5.1.1 QE-5.1.2
!      CALL grid_gather( f, flocal )
!Compatible with QE-svn
      CALL gather_grid( dfftp, f%of_r, flocal )
      CALL mp_sum( flocal, intra_bgrp_comm )
#else
      flocal = f%of_r
#endif
      !
      IF( ionode ) THEN
        !
        OPEN( 300, file = TRIM( filename ), status = 'unknown' )
        !
        origin=0.d0
        scale=alat!*0.52917720859d0
        WRITE(300,*)'CUBE FILE GENERATED BY PW.X'
        WRITE(300,*)'OUTER LOOP: X, MIDDLE LOOP: Y, INNER LOOP: Z'
        WRITE(300,'(i5,3f12.6)')nat,origin(1),origin(2),origin(3)
        WRITE(300,'(i5,3f12.6)')nr1,(at(ipol,1)/DBLE(nr1)*scale,ipol=1,3)
        WRITE(300,'(i5,3f12.6)')nr2,(at(ipol,2)/DBLE(nr2)*scale,ipol=1,3)
        WRITE(300,'(i5,3f12.6)')nr3,(at(ipol,3)/DBLE(nr3)*scale,ipol=1,3)
        DO iat=1,nat
           typ=ityp(iat)
           num=ions%iontype(typ)%atmnum
           WRITE(300,'(i5,4f12.6)')num,0.d0,tau(1,iat)*scale,&
                                  tau(2,iat)*scale,tau(3,iat)*scale
        ENDDO
        count=0
        DO ir1=1,nr1
          DO ir2=1,nr2
            DO ir3=1,nr3
              count=count+1
              ir = ir1 + ( ir2 -1 ) * nr1 + ( ir3 - 1 ) * nr1 * nr2
              tmp = DBLE( flocal( ir ) )
              IF (ABS(tmp).LT.1.D-99) tmp = 0.D0
              IF (MOD(count,6).EQ.0) THEN
                WRITE(300,'(e12.6,1x)')tmp
              ELSE
                WRITE(300,'(e12.6,1x)',advance='no')tmp
              ENDIF
            ENDDO
          ENDDO
        ENDDO
        !
        CLOSE( 300 )
        !
      END IF
      !
      DEALLOCATE( flocal )
      !
      RETURN
      !
!--------------------------------------------------------------------
      END SUBROUTINE write_cube
!--------------------------------------------------------------------

END MODULE environ_debug
